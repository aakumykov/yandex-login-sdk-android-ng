plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'maven-publish'
}

project.ext {
    artifactId = 'yandex-login-sdk-ng'
    groupId = 'com.github.akumykov'

    majorVersionNum = 3
    minorVersionNum = 1
    patchVersionNum = 0

    versionCode = 1

    versionName = majorVersionNum + "." + minorVersionNum + "." + patchVersionNum
}

android {
    namespace "com.yandex.authsdk"
    compileSdk 35

    defaultConfig {
        minSdk 21

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"

        manifestPlaceholders = [YANDEX_OAUTH_HOST: "oauth.yandex.ru"]

        def addConstant = { constantName, constantValue ->
            manifestPlaceholders += [(constantName): constantValue]
            buildConfigField "String", "${constantName}", "\"${constantValue}\""
        }
        addConstant("ACTION_YA_SDK_LOGIN", "com.yandex.auth.action.YA_SDK_LOGIN")
    }

    buildFeatures {
        buildConfig true
    }

    testVariants.all {
        it.mergedFlavor.manifestPlaceholders += [
                YANDEX_CLIENT_ID: "fcdddf83a97843ae80815c1c9247015b",
        ]
    }

    unitTestVariants.all {
        it.mergedFlavor.manifestPlaceholders += [
                YANDEX_CLIENT_ID: "fcdddf83a97843ae80815c1c9247015b",
        ]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

dependencies {
    implementation libs.androidx.core.ktx
    implementation libs.androidx.appcompat
    implementation libs.material
    testImplementation libs.junit

    implementation libs.androidx.annotation
    implementation libs.androidx.activity.ktx

    implementation libs.androidx.browser
    testImplementation libs.support.annotations
    testImplementation libs.robolectric
    testImplementation libs.assertj.core
    testImplementation libs.mockito.core
    testImplementation libs.mockk
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = project.groupId
            artifactId = project.artifactId
            version = versionName
            afterEvaluate {
                from components.release
            }
        }
    }
}